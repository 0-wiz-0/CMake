
cmake_minimum_required(VERSION 3.0)

project(CompileFeatures)

macro(run_test feature)
  if (";${CMAKE_CXX_COMPILE_FEATURES};" MATCHES ${feature})
    add_library(test_${feature} OBJECT ${feature}.cpp)
    set_property(TARGET test_${feature}
      PROPERTY COMPILE_FEATURES "${feature}"
    )
  else()
    list(APPEND non_features ${feature})
  endif()
endmacro()

foreach(feature ${CMAKE_CXX_KNOWN_FEATURES})
  run_test(${feature})
endforeach()

if (CMAKE_CXX_COMPILE_FEATURES)
  include(CheckCXXSourceCompiles)
  foreach(feature ${non_features})
    check_cxx_source_compiles("#include \"${CMAKE_CURRENT_SOURCE_DIR}/${feature}.cpp\"int main() { return 0; }\n" ${feature}_works)
    if (${feature}_works)
      message(SEND_ERROR
        "Feature ${feature} expected not to work for ${CMAKE_CXX_COMPILER_ID}-${CMAKE_CXX_COMPILER_VERSION}.  Update the supported features or blacklist it.")
    endif()
  endforeach()
endif()

add_executable(CompileFeatures main.cpp)
set_property(TARGET CompileFeatures
  PROPERTY COMPILE_FEATURES "cxx_delegating_constructors"
)

add_executable(GenexCompileFeatures main.cpp)
set_property(TARGET GenexCompileFeatures
  PROPERTY COMPILE_FEATURES "$<1:cxx_delegating_constructors>;$<0:not_a_feature>"
)

add_library(iface INTERFACE)
set_property(TARGET iface
  PROPERTY INTERFACE_COMPILE_FEATURES "cxx_delegating_constructors"
)
add_executable(IfaceCompileFeatures main.cpp)
target_link_libraries(IfaceCompileFeatures iface)
