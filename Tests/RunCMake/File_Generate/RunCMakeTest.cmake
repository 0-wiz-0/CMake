include(RunCMake)

run_cmake(CommandConflict)
if("${RunCMake_GENERATOR}" MATCHES "Visual Studio" OR "${RunCMake_GENERATOR}" MATCHES "XCode" )
    run_cmake(OutputConflict)
endif()
run_cmake(EmptyCondition1)
run_cmake(EmptyCondition2)
run_cmake(BadCondition)
run_cmake(DebugEvaluate)

set(timeformat "%Y%j%H%M%S")

macro(prepare_CMP0053_test policyStatus)
  file(REMOVE "${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt")
  set(RunCMake_TEST_FILE "${RunCMake_SOURCE_DIR}/CMP0053-${policyStatus}")
  set(RunCMake_TEST_BINARY_DIR "${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build")
  run_cmake(CMP0053-${policyStatus}-prepare)
  unset(RunCMake_TEST_FILE)
  unset(RunCMake_TEST_BINARY_DIR)
  file(TIMESTAMP "${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt" timestamp_${policyStatus} ${timeformat})
  if(NOT timestamp_${policyStatus})
    message(SEND_ERROR "Could not get timestamp for \"${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt\"")
  endif()
endmacro()

prepare_CMP0053_test(OLD)
prepare_CMP0053_test(NEW)
prepare_CMP0053_test(WARN)

execute_process(COMMAND ${CMAKE_COMMAND} -E sleep 1)

macro(run_CMP0053_test policyStatus)
  run_cmake(CMP0053-${policyStatus})
  file(TIMESTAMP "${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt" timestamp_${policyStatus}_after ${timeformat})
  if(NOT timestamp_${policyStatus}_after)
    message(SEND_ERROR "Could not get timestamp for \"${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt\"")
  endif()
endmacro()

set(RunCMake_TEST_NO_CLEAN ON)
run_CMP0053_test(OLD)
run_CMP0053_test(NEW)
run_CMP0053_test(WARN)
unset(RunCMake_TEST_NO_CLEAN)

if (timestamp_OLD_after STREQUAL timestamp_OLD)
  message(SEND_ERROR "CMP0053 OLD behavior did not change output file.")
endif()
if (NOT timestamp_NEW_after STREQUAL timestamp_NEW)
  message(SEND_ERROR "CMP0053 NEW behavior changed output file. Before: ${timestamp_NEW}, After: ${timestamp_NEW_after}")
endif()
if (timestamp_WARN_after STREQUAL timestamp_WARN)
  message(SEND_ERROR "CMP0053 WARN behavior did not change output file.")
endif()
