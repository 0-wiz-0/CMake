include(RunCMake)

run_cmake(CommandConflict)
if("${RunCMake_GENERATOR}" MATCHES "Visual Studio" OR "${RunCMake_GENERATOR}" MATCHES "XCode" )
    run_cmake(OutputConflict)
endif()
run_cmake(EmptyCondition1)
run_cmake(EmptyCondition2)
run_cmake(BadCondition)
run_cmake(DebugEvaluate)

set(timeformat "%Y%j%H%M%S")

macro(prepare_CMP0053_test policyStatus)
  set(RunCMake_TEST_FILE "${RunCMake_SOURCE_DIR}/CMP0053-${policyStatus}")
  run_cmake(CMP0053-${policyStatus}-prepare)
  unset(RunCMake_TEST_FILE)
  file(TIMESTAMP "${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt" timestamp_${policyStatus} ${timeformat})
  if(timestamp_${policyStatus} EQUAL 0)
    message(SEND_ERROR "Could not get timestamp for \"${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt\"")
  endif()
endmacro()

prepare_CMP0053_test(OLD)
prepare_CMP0053_test(NEW)
prepare_CMP0053_test(WARN)

macro(run_CMP0053_test policyStatus)
  set(RunCMake_TEST_OPTIONS "-DCHECK_GENERATE_TIMESTAMP=timestamp_${policyStatus}")
  run_cmake(CMP0053-${policyStatus})
  file(TIMESTAMP "${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt" timestamp_${policyStatus}_after ${timeformat})
  if(timestamp_${policyStatus}_after EQUAL 0)
    message(SEND_ERROR "Could not get timestamp for \"${RunCMake_BINARY_DIR}/CMP0053-${policyStatus}-build/output_file.txt\"")
  endif()
endmacro()

set(RunCMake_TEST_NO_CLEAN ON)
run_CMP0053_test(OLD)
run_CMP0053_test(NEW)
run_CMP0053_test(WARN)
unset(RunCMake_TEST_NO_CLEAN)

if (timestamp_OLD_after STREQUAL timestamp_OLD)
  message(SEND_ERROR "CMP0053 OLD behavior did not change output file.")
endif()
if (NOT timestamp_NEW_after STREQUAL timestamp_NEW)
  message(SEND_ERROR "CMP0053 NEW behavior changed output file.")
endif()
if (timestamp_WARN_after STREQUAL timestamp_WARN)
  message(SEND_ERROR "CMP0053 WARN behavior did not change output file.")
endif()
